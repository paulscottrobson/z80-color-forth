; ***************************************************************************************************************
; ***************************************************************************************************************
;
;		Name : 		memory.core
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		8th December 2019
;		Purpose :	Z80 Colour Forth : Memory/Port I/O words
;
; ***************************************************************************************************************
; ***************************************************************************************************************

@@macro.ix 	!
	pop 	hl
	ex 		de,hl
	ld 		(hl),e
	inc 	hl
	ld 		(hl),d
	pop 	de
@end

; ***************************************************************************************************************

@@word.ix 	+!
	pop 	hl
	ld 		a,(de)
	add 	a,l
	ld 		(de),a
	inc 	de
	ld 		a,(de)
	adc 	a,h
	ld 		(de),a
	pop 	de
@end

; ***************************************************************************************************************

@@macro.ret 	@
	ex 		de,hl
	ld 		e,(hl)
	inc 	hl
	ld 		d,(hl)
@end

; ***************************************************************************************************************

@@macro.ret 	b@::c@
	ld 		a,(de)
	ld 		e,a
	ld 		d,0
@end

; ***************************************************************************************************************

@@macro.ix	b!::c!
	pop 	hl
	ld 		a,l
	ld 		(de),a
	pop 	de
@end

; ***************************************************************************************************************

@@word.ix 	or!
	pop 	hl
	ld 		a,(de)
	or 		l
	ld 		(de),a
	inc 	de
	ld 		a,(de)
	or 		h
	ld 		(de),a
	pop 	de
@end

; ***************************************************************************************************************

@@macro.ret 	p@
	ld 		c,e
	ld 		b,d
	in 		e,(c)
	ld 		d,0
@end

; ***************************************************************************************************************

@@macro.hl 	p!
	pop 	hl
	ld 		c,e
	ld 		b,d
	out 	(c),l
	pop 	de
@end

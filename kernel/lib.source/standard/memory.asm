; *********************************************************************************
; *********************************************************************************
;
;		File:		memory.asm
;		Purpose:	Memory access words
;		Date : 		28th December 2018
;		Author:		paul@robsons.org.uk
;
; *********************************************************************************
; *********************************************************************************

; *********************************************************************************
;									Standard words
; *********************************************************************************
	
@copier 	!
	pop 	de
	ld 		(hl),e
	inc 	hl
	ld 		(hl),d
	pop 	hl
@end

@copier 	b!
	pop 	de
	ld 		(hl),e
	pop 	hl
@end

@copier 	@
	ld 		a,(hl)
	inc 	hl
	ld 		h,(hl)
	ld 		l,a
@end

@copier 	b@
	ld 		l,(hl)
	ld 		h,0
@end

; *********************************************************************************
;									Add to memory
; *********************************************************************************

@word.ix 	+!
	pop 	de
	ld 		a,(hl)
	add 	a,e
	ld 		(hl),a
	inc 	hl
	ld 		a,(hl)
	adc 	a,d
	ld 		(hl),a
	pop 	hl
@end

; *********************************************************************************
;									Or to memory
; *********************************************************************************

@word.ix 	or!
	pop 	de
	ld 		a,(hl)
	or 		e
	ld 		(hl),a
	inc 	hl
	ld 		a,(hl)
	or 		d
	ld 		(hl),a
	pop 	hl
@end

